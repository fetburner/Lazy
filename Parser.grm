


%%
(* Declarations for ML-Yacc *)

%name Lazy
%pos unit
%eop SEMICOLON
%start EXP

(* Terminal symbols *)
%term IDENT of string (* value identifier *)
    | BOOL of bool
    | DIGIT of int
    | CASE (* case *)
    | ELSE (* else *)
    | END (* end *)
    | FN (* fn *)
    | IF (* if *)
    | IN (* in *)
    | LET (* let *)
    | OF (* of *)
    | REC (* rec *)
    | THEN (* then *)
    | VAL (* val *)
    | EQUAL (* = *)
    | COMMA (* , *)
    | LPAREN (* ( *)
    | RPAREN (* ) *)
    | FATARROW (* => *)
    | SEMICOLON (* ; *)
    | PLUS (* + *)
    | MINUS (* - *)
    | TIMES (* * *)
    | LE (* <= *)
    | LBRACKET (* [ *)
    | RBRACKET (* * *)
    | CONS (* :: *)
    | BAR (* | *)
    | AND (* and *)

(* Nonterminal symbols *)
%nonterm ATEXP of Syntax.exp
       | IDENTSEQ_AUX of string list
       | IDENTSEQ of string list
       | EXP of Syntax.exp
       | APPEXP of Syntax.exp
       | PRODEXP of Syntax.exp
       | SUMEXP of Syntax.exp
       | CONSEXP of Syntax.exp
       | CMPEXP of Syntax.exp
       | ATDEC of Syntax.dec
       | VALBIND of (string * Syntax.exp) list
       | DEC of Syntax.dec list
       | TUPLE_BODY of Syntax.exp list

%%

(* Production rules *)
ATEXP : BOOL
          (Syntax.BOOL BOOL)
      | DIGIT
          (Syntax.INT DIGIT)
      | LPAREN RPAREN
          (Syntax.TUPLE [])
      | LBRACKET RBRACKET
          (Syntax.NIL)
      | LPAREN TUPLE_BODY RPAREN
          (Syntax.TUPLE TUPLE_BODY)
      | IDENT
          (Syntax.VAR IDENT)
      | LET DEC IN EXP END
          (Syntax.LET (DEC, EXP))
      | LPAREN EXP RPAREN
          (EXP)

IDENTSEQ_AUX : IDENT
               ([IDENT])
           | IDENT COMMA IDENTSEQ_AUX
               (IDENT :: IDENTSEQ_AUX)

IDENTSEQ : LPAREN RPAREN
           ([])
       | IDENT
           ([IDENT])
       | LPAREN IDENTSEQ_AUX RPAREN
           (IDENTSEQ_AUX)

APPEXP : ATEXP
           (ATEXP)
       | APPEXP ATEXP
           (Syntax.APP (APPEXP, ATEXP))

PRODEXP : APPEXP
            (APPEXP)
        | PRODEXP TIMES APPEXP
            (Syntax.TIMES (PRODEXP, APPEXP))

SUMEXP : PRODEXP
           (PRODEXP)
       | SUMEXP PLUS PRODEXP
           (Syntax.PLUS (SUMEXP, PRODEXP))
       | SUMEXP MINUS PRODEXP
           (Syntax.MINUS (SUMEXP, PRODEXP))

CONSEXP : SUMEXP
            (SUMEXP)
        | SUMEXP CONS CONSEXP
            (Syntax.CONS (SUMEXP, CONSEXP))

CMPEXP : CONSEXP
           (CONSEXP)
       | CONSEXP LE CONSEXP
           (Syntax.LE (CONSEXP1, CONSEXP2))

EXP : CMPEXP
        (CMPEXP)
    | FN IDENT FATARROW EXP
        (Syntax.ABS (IDENT, EXP))
    | IF EXP THEN EXP ELSE EXP
        (Syntax.IF (EXP1, EXP2, EXP3))
    | CASE EXP OF IDENTSEQ FATARROW EXP
        (Syntax.LET_TUPLE (EXP1, IDENTSEQ, EXP2))
    | CASE EXP OF LBRACKET RBRACKET FATARROW EXP BAR IDENT CONS IDENT FATARROW EXP
        (Syntax.CASE (EXP1, EXP2, IDENT1, IDENT2, EXP3))

ATDEC : VAL IDENT EQUAL EXP
          (Syntax.VAL (IDENT, EXP))
      | VAL REC VALBIND
          (Syntax.VALREC (VALBIND))

VALBIND : IDENT EQUAL EXP
            ([(IDENT, EXP)])
        | IDENT EQUAL EXP AND VALBIND
	    ((IDENT, EXP) :: VALBIND)

DEC : ATDEC
        ([ATDEC])
    | ATDEC SEMICOLON DEC
        (ATDEC :: DEC)
    | ATDEC DEC
        (ATDEC :: DEC)

TUPLE_BODY : EXP COMMA EXP
               ([EXP1, EXP2])
           | EXP COMMA TUPLE_BODY
               (EXP :: TUPLE_BODY)
